#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
# 姓名：李孙木铎
# 学号：2023010304
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
# Procedure registers info: 
#  %rax - count of positive words
# Remaining registers available for use:
#  %r8-%r14, %rbx, %rbp, %rcx
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	# xorq %rax,%rax		# count = 0;
	# andq %rdx,%rdx		# len <= 0?
	iaddq $-9, %rdx		# len -= 10
	jl Remainder 	# if so, goto Remainder

Loop:
	# apply the loop unrolling optimization with factor 10
	mrmovq (%rdi), %r8 		# read first val from src...
	mrmovq 8(%rdi), %r9 	# read second val from src...
	mrmovq 16(%rdi), %r10	# read third val from src...
	mrmovq 24(%rdi), %r11	# read fourth val from src...
	mrmovq 32(%rdi), %r12	# read fifth val from src...
	mrmovq 40(%rdi), %r13	# read sixth val from src...
	mrmovq 48(%rdi), %r14	# read seventh val from src...
	mrmovq 56(%rdi), %rbx	# read eighth val from src...
	mrmovq 64(%rdi), %rcx	# read ninth val from src...

	rmmovq %r8, (%rsi) 		# ...store first to dst
	rmmovq %r9, 8(%rsi) 	# ...store second to dst
	rmmovq %r10, 16(%rsi)	# ...store third to dst
	rmmovq %r11, 24(%rsi)	# ...store fourth to dst
	rmmovq %r12, 32(%rsi)	# ...store fifth to dst
	rmmovq %r13, 40(%rsi)	# ...store sixth to dst
	rmmovq %r14, 48(%rsi)	# ...store seventh to dst
	rmmovq %rbx, 56(%rsi)	# ...store eighth to dst
	rmmovq %rcx, 64(%rsi)	# ...store ninth to dst
	

Npos0:
	andq %r8, %r8		# val0 <= 0?
	jle Npos1		# if so, goto Npos0:
	iaddq $1, %rax 		# count++
Npos1:
	andq %r9, %r9		# val1 <= 0?
	jle Npos2		# if so, goto Npos1:
	iaddq $1, %rax 		# count++
Npos2:
	andq %r10, %r10		# val2 <= 0?
	jle Npos3		# if so, goto Npos2:
	iaddq $1, %rax 		# count++
Npos3:
	andq %r11, %r11		# val3 <= 0?
	jle Npos4		# if so, goto Npos3:
	iaddq $1, %rax 		# count++
Npos4:
	andq %r12, %r12		# val4 <= 0?
	jle Npos5		# if so, goto Npos4:
	iaddq $1, %rax 		# count++
Npos5:
	andq %r13, %r13		# val5 <= 0?
	jle Npos6		# if so, goto Npos5:
	iaddq $1, %rax 		# count++
Npos6:
	andq %r14, %r14		# val6 <= 0?
	jle Npos7		# if so, goto Npos6:	
	iaddq $1, %rax 		# count++
Npos7:
	andq %rbx, %rbx		# val7 <= 0?
	jle Npos8		# if so, goto Npos7:
	iaddq $1, %rax 		# count++
Npos8:
	andq %rcx, %rcx		# val8 <= 0?
	jle Npos9		# if so, goto Npos8:
	iaddq $1, %rax 		# count++
Npos9:
	# Loop update
	iaddq $72, %rdi			# src += 8 * 10
	iaddq $72, %rsi			# dst += 8 * 10
	iaddq $-9, %rdx		# len -= 10
	jge Loop		# if so, goto Loop:

# Remainder handling
# rdx now ranges from -9 to -1
Remainder:
    iaddq $6, %rdx          # <0 (-9..-7 -> 剩0..2)
    jl Rem_0_2

Rem_3_8: # rdx now ranges from 0 to 5
	iaddq $-3, %rdx         # <0 (0..2 -> 剩3..5)
	jl Rem_3_5

Rem_6_8: # rdx now ranges from 0 to 2
	iaddq $-1, %rdx         # rdx now ranges from -1 to 1
	mrmovq 40(%rdi), %r13
	jl R6 					# <0 (剩6)
	mrmovq 48(%rdi), %r14
	je R7					# =0 (剩7)
	mrmovq 56(%rdi), %rbx
	jg R8					# >0 (剩8)

Rem_3_5: # rdx now ranges from -3 to -1
	iaddq $2, %rdx			# rdx now ranges from -1 to 1
	mrmovq 16(%rdi), %r10
	jl R3					# <0 (剩3)
	mrmovq 24(%rdi), %r11
	je R4					# =0 (剩4)
	mrmovq 32(%rdi), %r12
	jg R5					# >0 (剩5)

Rem_0_2: # rdx now ranges from -3 to -1
    iaddq $2, %rdx          # rdx now ranges from -1 to 1
	mrmovq (%rdi), %r8
    je R1              		# =0 (剩1)
	mrmovq 8(%rdi), %r9
    jg R2                   # >0 (剩2)
    ret 					# 剩0，Done

R8: # handle 8th remaining element
	andq %rbx, %rbx
	rmmovq %rbx, 56(%rsi)
	mrmovq 48(%rdi), %r14
	jle R7
	iaddq $1, %rax
R7: # handle 7th remaining element
	andq %r14, %r14
	rmmovq %r14, 48(%rsi)
	mrmovq 40(%rdi), %r13	
	jle R6
	iaddq $1, %rax
R6: # handle 6th remaining element
	andq %r13, %r13
	rmmovq %r13, 40(%rsi)
	mrmovq 32(%rdi), %r12
	jle R5
	iaddq $1, %rax
R5: # handle 5th remaining element
	andq %r12, %r12
	rmmovq %r12, 32(%rsi)
	mrmovq 24(%rdi), %r11
	jle R4
	iaddq $1, %rax
R4: # handle 4th remaining element
	andq %r11, %r11
	rmmovq %r11, 24(%rsi)
	mrmovq 16(%rdi), %r10
	jle R3
	iaddq $1, %rax
R3: # handle 3rd remaining element
	andq %r10, %r10
	rmmovq %r10, 16(%rsi)
	mrmovq 8(%rdi), %r9
	jle R2
	iaddq $1, %rax
R2: # handle 2nd remaining element
	andq %r9, %r9
	rmmovq %r9, 8(%rsi)
	mrmovq (%rdi), %r8
	jle R1
	iaddq $1, %rax
R1: # handle 1st remaining element
	andq %r8, %r8
	rmmovq %r8, (%rsi)
	jle Done
	iaddq $1, %rax
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
